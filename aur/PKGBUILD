# Maintainer: James Knight <james.d.knight@live.com>

pkgbase=releng-tool
pkgname=(releng-tool python2-releng-tool)
pkgver=0.9.0.dev0
_pkgtarget=commit=aaf67120d70b0c667ae430cc70af59728b1c9cea # tmp: tag=v$pkgver
pkgrel=1
pkgdesc='Tool to assist in the release engineering of a project'
url=https://releng.io/
arch=(any)
license=(BSD)
makedepends=(
  git
  python-setuptools
  python2-setuptools
)
optdepends=(
  'bash-completion: autocomplete for bash'
  'fish: autocomplete for fish'
  'zsh-completions: autocomplete for zsh'
)
source=($pkgname-$pkgver::git+https://github.com/releng-tool/releng-tool.git#$_pkgtarget)
sha512sums=(SKIP)

prepare() {
  cp -a $pkgbase-$pkgver{,-py2}

  cd $pkgbase-$pkgver-py2/releng
  sed 's_#!/usr/bin/env python_#!/usr/bin/env python2_' \
      -i $(find . -name '*.py')
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build

  cd "$srcdir/$pkgbase-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py test --test-suite tests.unit-tests

  cd "$srcdir/$pkgbase-$pkgver-py2"
  python2 setup.py test --test-suite tests.unit-tests
}

package_releng-tool() {
  depends=(python)
  cd $pkgbase-$pkgver

  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build --verbose

  install -vDm644 scripts/completion/bash \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 scripts/completion/fish \
    "$pkgdir/usr/share/fish/completions/$pkgname.fish"
  install -vDm644 scripts/completion/zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -vDm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-releng-tool() {
  depends=(python2)
  cd $pkgbase-$pkgver-py2

  export PYTHONHASHSEED=0
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build --verbose

  mv "$pkgdir/usr/bin/releng-tool" "$pkgdir/usr/bin/$pkgname"

  install -vDm644 scripts/completion/bash \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm644 scripts/completion/fish \
    "$pkgdir/usr/share/fish/completions/$pkgname.fish"
  install -vDm644 scripts/completion/zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -vDm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  local completion_scripts=(
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
    "$pkgdir/usr/share/fish/completions/$pkgname.fish"
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  )
  for completion_script in ${completion_scripts[*]}; do
      sed -i s/releng-tool/$pkgname/ "$completion_script"
      sed -i s/_releng_tool/_releng_tool_py2/ "$completion_script"
  done
}
